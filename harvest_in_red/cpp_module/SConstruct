import os

env = SConscript(os.path.join('..', '..', 'godot-cpp', 'SConstruct'))

env.Append(CPPPATH=['src'])

sources = ['register_types.cpp'] + Glob('src/*.cpp')

if env['platform'] == 'macos':
    lib = env.SharedLibrary(
        'bin/libexample.{}.{}.framework/libexample.{}.{}'.format(
            env['platform'], env['target'], env['platform'], env['target']
        ),
        source=sources,
    )
elif env['platform'] == 'ios':
    if env['ios_simulator']:
        lib = env.StaticLibrary(
            'bin/libexample.{}.{}.simulator{}'.format(
                env['platform'], env['target'], env['LIBSUFFIX']
            ),
            source=sources,
        )
    else:
        lib = env.StaticLibrary(
            'bin/libexample.{}.{}{}'.format(
                env['platform'], env['target'], env['LIBSUFFIX']
            ),
            source=sources,
        )
else:
    lib = env.SharedLibrary(
        'bin/libexample{}{}'.format(env['suffix'], env['SHLIBSUFFIX']),
        source=sources,
    )

env.NoCache(lib)
Default(lib)
